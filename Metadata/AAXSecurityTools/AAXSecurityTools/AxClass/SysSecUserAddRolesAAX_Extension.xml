<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SysSecUserAddRolesAAX_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(formStr(SysSecUserAddRoles))]
final class SysSecUserAddRolesAAX_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>assignRolesToSelectedUser</Name>
				<Source><![CDATA[
    public void assignRolesToSelectedUser()
    {
        OMUserRoleOrganization org;
        SysUserInfo sysUserInfo;
        SecurityUserRole userRole;
        SecurityUserRoleCondition userRoleCondition;
        OMLegalEntity legalEntity;
        SecurityRole secRole;
        Set companyList = new Set(Types::Int64);
        SetEnumerator se;
        str outputCompanyList = "User role assignment restricted to the following companies: ";

        //execute base method
        next assignRolesToSelectedUser();

        //get
        str currentUser = curUserId();
        str _userId = userId;
        int64 _securityRoleId = securityRole.RecId;
        int64 _userSecurityRoleId = 0;
        
        //get company restrictions assigned to user performing user role assignment
        while select OmHierarchyType, OmInternalOrganization from org
            join sysUserInfo
            where sysUserInfo.CompanyRestrictions == NoYes::Yes
            && sysUserInfo.Id == currentUser
            && org.User == currentUser
        {
            companyList.add(org.OMInternalOrganization);
        }

        //get role(s) assigned to user
        select secRole where secRole.RecId == _securityRoleId;
        securityRole_ds.setRecord(securityRole);

        while(secRole)
        {
            select RecId, SecurityRole, User from userRole
                where userRole.SecurityRole == secRole.RecId &&
                    userRole.User == _userId;
            
            if(userRole)
            {
                _userSecurityRoleId = userRole.RecId;

                //now assign company restrictions
                if(companyList.elements() > 0)
                {
                    se = companyList.getEnumerator();
               
                    ttsbegin;
                    while(se.moveNext())
                    {
                        int64 compId = se.current();

                        //OmUserRoleOrganization
                        org.User = _userId;
                        org.SecurityRole = _securityRoleId;
                        org.SecurityRoleAssignmentRule = 0;
                        org.omInternalOrganization = compId;
                        org.omHierarchyType = 0;
                        org.insert();

                        //SecurityUserRoleCondition
                        select firstfast LegalEntityId from legalEntity where legalEntity.RecId == compId;
                        if(legalEntity)
                        {
                            str comp = legalEntity.LegalEntityId;

                            userRoleCondition.SecurityUserRole = _userSecurityRoleId;
                            userRoleCondition.DataArea = comp;
                            userRoleCondition.insert();

                            outputCompanyList = strFmt("%1 %2,", outputCompanyList, comp);
                        }
                    }
                    ttscommit;
                }

            }

            secRole = securityRole_ds.getNext() as SecurityRole;
        }

        if(companyList.elements() > 0)
        {
            info(outputCompanyList);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>